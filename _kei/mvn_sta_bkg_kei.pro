;+
;FUNCTION:	mvn_sta_c6_bkg(dat)
;INPUT:	
;	dat:	structure,	generated by mvn_sta_get_c6() 
;KEYWORDS
;	
;PURPOSE:
;	Returns the background for maven static apid c6 structures 
;NOTES:	
;	Function to calculate the straggling backgroud from maven static apid c6 packets
;	Function can be used as "bkg" keyword passed to get_4dt.pro for background subtraction
;
;	This generates a decent, energy independent background subtraction if coincident events can be ignored
;		proton straggling represents about ~2% of the events when coincident events can be ignored
;
;CREATED BY:
;	J.McFadden	14-05-08	
;LAST MODIFICATION:
;-
function mvn_sta_bkg_kei,dat


;; calculate proton contamination for c6 data
if dat.apid eq 'c6' then begin
	bg = fltarr(32,64) ;;32e x 64m
	bb = total(dat.data[*,0:6],2)/total(dat.twt_arr[*,0:6],2) ;;proton
	maxt = max(bb,ind)          ;ind corresponds the energy channel where counts are maximum
	ind = ind>2									; handles bb[*]=0
	for ii=8,63 do bg[*,ii] = bb * dat.data[ind,ii]/(bb[ind]>1.e-10)
	cc = total(dat.data[*,9:13],2)/total(dat.twt_arr[*,9:13],2) ;;alpha
	for ii=15,63 do bg[*,ii] = bg[*,ii] + cc * dat.data[ind-2,ii]/(cc[ind-2]>1.e-10) 
endif


if dat.apid eq 'd0' or dat.apid eq 'd1' then begin
bg = fltarr(32,64,8)

  for in=0,63 do begin
    bb = dat.data[*,in,0]/dat.twt_arr[*,in,0]
    maxt = max(bb,ind)
    ind = ind>2
    for ii=1,7 do bg[*,in,ii] = bb * dat.data[ind,in,ii]/(bb[ind]>1.e-10)
    cc = dat.data[*,in,1]/dat.twt_arr[*,in,0]
    for ii=2,7 do bg[*,in,ii] = bg[*,in,ii] + cc * dat.data[ind-2,in,ii]/(cc[ind-2]>1.e-10)
  endfor
endif


	return,bg

end

